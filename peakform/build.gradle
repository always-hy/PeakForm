plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.sustech.cs304.project'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'io.github.cdimascio:java-dotenv:5.2.2'
    implementation 'com.google.firebase:firebase-admin:9.2.0'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.alipay.sdk:alipay-sdk-java:4.40.165.ALL'
    implementation 'com.stripe:stripe-java:24.20.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:testcontainers:1.20.6'
    testImplementation 'org.testcontainers:mysql:1.20.6'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.6'
    testImplementation 'org.testcontainers:rabbitmq:1.20.6'
    testImplementation 'com.icegreen:greenmail:1.6.5'
    testImplementation 'io.rest-assured:rest-assured:5.5.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
//    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    dependsOn test
}

tasks.register('startDockerCompose', Exec) {
    group = 'Docker'
    description = 'Start Docker Compose services'
    commandLine 'docker', 'compose', 'up', '-d'
}

tasks.register('stopDockerCompose', Exec) {
    group = 'Docker'
    description = 'Stop Docker Compose services'
    commandLine 'docker', 'compose', 'down'
}

tasks.named('bootRun') {
    dependsOn startDockerCompose
    finalizedBy stopDockerCompose
}